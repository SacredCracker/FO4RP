#include "_macros.fos"

#define State		Val0

void _InitFurniture( Item& furniture, bool firstTime )
{
    furniture.SetEvent( ITEM_EVENT_SKILL, "_UseFurniture" );
}

bool _UseFurniture( Item& furniture, Critter& cr, int skill )
{
    switch( skill )
	{
		case( SK_TRAPS ): return Rotate( furniture, false );
		case( SK_SCIENCE ): return SwitchLootability( cr, furniture );
		case( SK_REPAIR ): return Rotate( furniture );
		default: return false;
	}
	return false;
}

bool SwitchLootability( Critter& cr, Item& furniture )
{
	string state = "";
	if( !FLAG( furniture.Flags, ITEM_CAN_PICKUP ) )
	{
		SETFLAG( furniture.Flags, ITEM_CAN_PICKUP );
		state = "поднимаем";
	}
	else 
	{
		UNSETFLAG( furniture.Flags, ITEM_CAN_PICKUP );
		state = "неподъёмен";
	}
	
	cr.Say( SAY_NETMSG, "Предмет " + state + ". Что бы изменить состояние - используйте науку." );	
	return true;
}

//import bool Rotate( Item& furniture, bool clockwise = true ) from "furniture";
bool Rotate( Item& furniture, bool clockwise = true ) //exported
{
	InitFurnitures();
	return furnitures.rotate( furniture, clockwise );
}

FurnitureList@ furnitures;
void InitFurnitures()
{
	if( valid( furnitures ) ) return;
	//TODO: Нормально проименовать все эти циферки пидов сценери.
	@furnitures = @CreateFurniture( "Единый список мебели")
	//	.add( Furniture( "Имя для скрипта", PID_крафтаемого_объекта, изображения "при повороте" ) )
		.add( Furniture( "Деревянный стул", PID_CR_WOOD_CHAIR ).add(2666).add(2670).add(2676).add(2663) )
		.add( Furniture( "Удобный стул", PID_CR_CMFRT_CHAIR ).add(2667).add(2672).add(2677).add(2662) )
		.add( Furniture( "Металлический стул", PID_CR_METAL_CHAIR ).add(2668).add(2673).add(2674).add(2664) )
		.add( Furniture( "Стул с тканевой обивкой", PID_CR_RAGS_CHAIR ).add(2669).add(2671).add(2675).add(2665) )
		.add( Furniture( "Спальный мешок", PID_CR_SLP_BAG ).add(2208).add(2209) )
		.add( Furniture( "Матрас", PID_CR_MTRS ).add(2206).add(2207) )
		.add( Furniture( "Двойная кровать", PID_CR_BD_DBL_BED ).add(2210).add(2211) )
		.add( Furniture( "Простенькая кровать'", PID_CR_JNK_BED ).add(2211).add(2212) )
		.add( Furniture( "Лежак", PID_CR_SUN_BED).add(2948).add(2949) )
		.add( Furniture( "Удобная кровать", PID_CR_WOOD_BED ).add(2678).add(2680) )
		.add( Furniture( "Роскошная кровать", PID_CR_LUX_BED   ).add(2679).add(2681) )
		.add( Furniture( "Деревянный стол", PID_CR_WOOD_TBL  ).add(2084) )
		.add( Furniture( "Деревянный стол", PID_CR_WOOD_TBL_GD ).add(2659) )
		.add( Furniture( "Небольшой металлический стол", PID_CR_MT_TBL_SMALL ).add(2660) )
		.add( Furniture( "Овальный металлический стол", PID_CR_MT_TBL_OVAL ).add(2661) )
		.add( Furniture( "Большой металлический стол", PID_CR_MT_TBL_BG ).add(2604).add(2606) )
		.add( Furniture( "Большой металлический стол с покрытием", PID_CR_MT_TBL_BG_ALT ).add(2605).add(2607) )
		.add( Furniture( "Металлический рабочий стол", PID_CR_MT_TBL_WR ).add(185).add(187) )
		.add( Furniture( "Ржавый рабочий стол", PID_CR_JNK_TBL_WR ).add(183).add(184) )
		.add( Furniture( "Большой деревянный стол", PID_CR_WOOD_TBL_DINNER ).add(166).add(167) )
		;
		
	
}


#ifndef __FURNITURE__TESTS
	#define __FURNITURE__TESTS
	
	//~run furniture go 0 0 0
	void go( Critter& cr, int p0, int p1, int p2 )
	{
		cr.Say( SAY_NETMSG, "Put firniture tests here." );
		TestUse( cr, p0, p1 );
	}

	void TestUse( Critter& cr, int furniture_id, int skill )
	{
		Item@ target = GetItem( furniture_id );
		if( !valid( target ) )
		{
			cr.Say( SAY_NETMSG, "Предмет не найден." );
			return;
		}
		
		if( _UseFurniture( target, cr, skill ) )
		{
			cr.Say( SAY_NETMSG, "Тест успешен, дополнительная информация могла быть здесь." );
		}
		else
		{
			cr.Say( SAY_NETMSG, "Тест провален, дополнительная информация могла быть здесь." );
		}
	}
#endif

#ifndef __FURNITURE__CLASSES
	#define __FURNITURE__CLASSES
	
	FurnitureList@ CreateFurniture( string name )
	{
		return FurnitureList( name );
	}
	
	class FurnitureList
	{
		string name;
		Furniture@[] list;
		
		FurnitureList( string name )
		{
			this.name = name;
		}
		
		FurnitureList@ add( Furniture@ item )
		{
			list.insertLast( item );
			return this;
		}
		
		bool rotate( Item& furniture, bool clockwise = true )
		{
			for( uint i = 0; i < list.length(); i++ )
				if( list[i].Rotate( furniture, clockwise ) )
					return true;
				
			return false;
		}
	}

	Furniture@ CreateFurniture( string name, uint basePID )
	{
		return Furniture( name, basePID );
	}

	class Furniture
	{
		uint basePID;
		string name;
		uint[] pids;
		
		Furniture( string name, uint basePID )
		{
			this.name = name;
			this.basePID = basePID;
		}
		
		void set( uint[] pids )
		{
			this.pids = pids;
		}
		
		Furniture@ add( uint rotation_picture_pid )
		{
			if( rotation_picture_pid != 0 )
				pids.insertLast( rotation_picture_pid );
			
			return this;
		}
		
		bool setPic( Item& target )
		{
			return setPic( target, target.State );
		}

		bool setPic( Item& target, uint index )
		{
			if( index >= pids.length() ) 
				return false;
			
			return setItemPic( target, pids[index] );
		}
		
		bool ChangeState( Item& target, bool clockwise = true )
		{
			if( clockwise )
				target.State++;
			else
				target.State--;

			int state = target.State;
			
			if( state >= pids.length() )
			{
				target.State = 0;
				return true;
			}
			
			if( state < 0 )
			{
				target.State = pids.length() - 1;
				return true;
			}
			
			return true;
		}
		
		bool Rotate( Item& target, bool clockwise = true )
		{
			if( target.GetProtoId() != basePID || pids.length() == 0 ) return false;
			
			ChangeState( target, clockwise );
			setPic( target );
			return true;
		}
	}
#endif

#ifndef __FURNITURE__UTILS
	#define __FURNITURE__UTILS
	
	bool setItemPic( Item& target, uint protoPID )
	{
		if( protoPID == 0 ) return false;
		
		ProtoItem@ proto = GetProtoItem( protoPID );
		if( !valid( proto ) ) return false;
		
		target.PicMap = proto.PicMap;
		target.Update();
		return true;
	}
#endif